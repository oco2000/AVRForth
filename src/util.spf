\ ---------------- debug messages ----------------
0 VALUE DEBUG-LEVEL \ debug level: 0..3

: ?DEBUG ( level -- f ) \ true is debug is on for the level
  DEBUG-LEVEL DUP IF > 0= ELSE NIP THEN
;

: LEVEL ( level -- )
  CREATE , IMMEDIATE
  DOES> @ TO DEBUG-LEVEL
;

\ level 0 - no debug messages
0 LEVEL -DEBUG  \ turn off the messages
\ level 1 - user messages
1 LEVEL +DEBUG  \ turn on level 1
1 LEVEL +DEBUG1 \ turn on level 1
2 LEVEL +DEBUG2 \ turn on level 2
\ level 2 - optimizer messages
3 LEVEL +DEBUG3 \ turn on level 3

: [THEN]   \ redefine to use in compilation state
  STATE @ IF \ компиляция
    POSTPONE THEN
  THEN
; IMMEDIATE

: CONDITION ( level -- )  \ conditional operator for the debug level
  CREATE , IMMEDIATE
  DOES> @
    STATE @ IF
      POSTPONE LITERAL POSTPONE ?DEBUG  POSTPONE IF
    ELSE
      ?DEBUG POSTPONE [IF]
    THEN
;

1 CONDITION [IFDEBUG]   \ if debug level 1 is on
1 CONDITION [IFDEBUG1]  \ if debug level 1 is on
2 CONDITION [IFDEBUG2]  \ if debug level 2 is on
3 CONDITION [IFDEBUG3]  \ if debug level 3 is on

VECT (T.)

: CONDITION_STRING ( level -- )  \ type a message if debug level is "level"
  CREATE , IMMEDIATE
  DOES>
    @ POSTPONE LITERAL
    POSTPONE ?DEBUG
    POSTPONE IF
      POSTPONE (T.)
      POSTPONE SPACE
      POSTPONE ."      \ "
      POSTPONE CR
    POSTPONE THEN
;

1 CONDITION_STRING DEBUG"    \ type string if debug level 1 is on
1 CONDITION_STRING DEBUG1"   \ type string if debug level 1 is on
2 CONDITION_STRING DEBUG2"   \ type string if debug level 2 is on
3 CONDITION_STRING DEBUG3"   \ type string if debug level 3 is on
\ -------------------------------------------------------

: 2CONSTANT ( x1 x2 "<spaces>name" -- )
  CREATE  , ,  DOES> 2@
;

: H.  BASE @ HEX SWAP U. BASE ! ;
: H.N           ( n1 n2 -- )    \ display n1 as a HEX number of n2 digits
    BASE @ >R HEX >R
    0 <# R> 0 ?DO # LOOP #> TYPE
    R> BASE ! ;
: .HEXSTACK
  BASE @ >R HEX .S CR R> BASE !
;

: TAB   0x9 EMIT ;

\ fix SP-FORTH bug, separate "-" is interpreted as 0
: ?SLITERAL3 ( c-addr u -- ... )
  2DUP 1 = SWAP C@ [CHAR] - = AND IF -2003 THROW THEN
  ?SLITERAL2
;

' ?SLITERAL3 TO ?SLITERAL

\ halt on errors
: ?HALT
  ?DUP IF DUP ERROR2 HALT THEN ;

' ?HALT TO ERROR \ set global error handler

C" 2VARIABLE" FIND NIP 0=
[IF]
 : 2VARIABLE
  CREATE 0 , 0 ,
  DOES> ;
[THEN]

: BYTE-SPLIT    ( W -- LO8 HI8 )
  DUP 0xFF AND
  SWAP 8 RSHIFT 0xFF AND ;

: ?EMPTY ( -- ) \ check the stack
    CR .S
    DEPTH 00 <> ABORT" Stack is not empty !!!"
;

: .BASE \ type the current base value
  BASE @ DUP DECIMAL ." BASE = " . BASE !
;

: NDROP ( x1 ... xn n -- ) \ drop n values
  DEPTH 0= IF ABORT" Stack is empty" THEN
  DEPTH 1- OVER < IF ABORT" Stack underflow" THEN
  0 ?DO DROP LOOP
;
\ ============================ bit operations ================================

: EXP2  ( u -- 2^u )
    1 SWAP LSHIFT ;

: SEQ_MASK ( pos n -- ) \ mask of n ones starting from the position pos
    EXP2 1- SWAP LSHIFT
;

: 1s        ( x -- n )      \ how many 1s are in byte x
  DUP 0x55 AND   SWAP 2/        0x55 AND   +
  DUP 0x33 AND   SWAP 2/ 2/     0x33 AND   +
  DUP 0x0F AND   SWAP 04 RSHIFT 0x0F AND   +
;

: LSB       ( x -- n )      \ position of the lowest significant bit
  0 ( x n )
  BEGIN
    OVER 1 AND 0=
  WHILE
    1+ SWAP 2/ SWAP
  REPEAT
  NIP
;

: RESET_LSB ( n -- n' )     \ reset the LSB
  DUP 1- AND
;

: EXTRACT_LSB ( n -- n' )   \ reset all bits except the LSB
  DUP 1- INVERT AND
;

\EOF
TESTCASES Utilities

    (( 00 00 SEQ_MASK -> 00 ))
    (( 00 01 SEQ_MASK -> 01 ))
    (( 01 05 SEQ_MASK -> 0x3E ))
    (( 05 03 SEQ_MASK -> 0xE0 ))

    (( 02 RESET_LSB -> 00 ))
    (( 03 RESET_LSB -> 02 ))
    (( 0x0A RESET_LSB -> 08 ))
    (( 0xFFFFFFF0 RESET_LSB -> 0xFFFFFFE0 ))

    (( 02 EXTRACT_LSB -> 02 ))
    (( 03 EXTRACT_LSB -> 01 ))
    (( 0xFFFFFFFF EXTRACT_LSB -> 01 ))
    (( 0xFFFFFFF0 EXTRACT_LSB -> 0x10 ))

END-TESTCASES
