\ HEX and BIN file creation

VARIABLE H-SUM
VARIABLE BASEADDR

: HEXBYTE ( BYTE FILE -- )
  >R DUP H-SUM @ + H-SUM !
  S>D <# # # #> R> WRITE-FILE THROW
;

: HEXLINE ( BASEADDR ADDR FILE -- )
  H-SUM 0!
  >R S" :" R@ WRITE-FILE THROW
  0x10 R@ HEXBYTE DUP 0x100 / R@ HEXBYTE
  DUP 0xFF AND R@ HEXBYTE 0x00 R@ HEXBYTE
  R> ( baseaddr addr file )
  0x10 0 DO
    >R 2DUP + C@ R@ HEXBYTE
    1+ R>
  LOOP
  >R 2DROP H-SUM @ NEGATE R@ HEXBYTE
  0 0 R> WRITE-LINE THROW
;

: HEXSAVE ( BASEADDR COUNT FILE ) \ HEX format
  BASE @ >R HEX
  SWAP 0 ?DO   ( baseaddr file )
    2DUP I SWAP HEXLINE
  0x10 +LOOP
  >R S" :00000001FF" R> WRITE-LINE THROW
  DROP
  R> BASE !
;

: SAVE ( BASEADDR COUNT 2FILENAME -- )
  R/W CREATE-FILE THROW >R
  R@ HEXSAVE
  R> CLOSE-FILE THROW
;

: BSAVE ( BASEADDR COUNT 2FILENAME -- ) \ binary format
  R/W CREATE-FILE THROW >R
  R@ WRITE-FILE THROW
  R> CLOSE-FILE THROW
;
