\ CASE

DECIMAL
VARIABLE   CSP    \ Control stack pointer
6 CONSTANT L-CAS# \ Maximum nesting level
CREATE     S-CSP   L-CAS# CELLS ALLOT \ Control stack
S-CSP CSP !

: +CSP ( -> P)    \ Next level
  CSP @ DUP CELL+ CSP !
;
: -CSP ( -> )     \ Previous level
  CSP @ 1 CELLS - CSP !
;
: !CSP ( -> )     \ Initialize level
  SP@ +CSP !
;
: CSP@ ( -> A)
  CSP @ 1 CELLS - @
;
: ?CSP ( -> )     \ Check nesting
  SP@ CSP@ <> 37 ?ERROR ( ABORT" CSP error!")
  -CSP
;
: CASE ( -> )
  !CSP
; IMMEDIATE
: OF
  POSTPONE OVER POSTPONE =
  [COMPILE] IF POSTPONE DROP
; IMMEDIATE
: ENDOF
  [COMPILE] ELSE
; IMMEDIATE
: ENDCASE
  POSTPONE DROP BEGIN SP@ CSP@ =
  0=  WHILE  [COMPILE] THEN  REPEAT -CSP
; IMMEDIATE
