\ 94 TOOLS

: .S ( -- ) \ 94 TOOLS
   DEPTH 0 MAX .SN
;

: ? ( a-addr -- ) \ 94 TOOLS
  @ .
;
: AHEAD  \ 94 TOOLS EXT
  HERE BRANCH, >MARK 2
; IMMEDIATE

: [ELSE]   \ 94 TOOLS EXT
    1
    BEGIN
      NextWord DUP
      IF
         2DUP S" [IF]"   COMPARE 0= IF 2DROP 1+                 ELSE
         2DUP S" [ELSE]" COMPARE 0= IF 2DROP 1- DUP  IF 1+ THEN ELSE
              S" [THEN]" COMPARE 0= IF       1-                 THEN
                                    THEN  THEN
      ELSE 2DROP REFILL  AND \   SOURCE TYPE
      THEN DUP 0=
    UNTIL  DROP ;  IMMEDIATE

: [IF] \ 94 TOOLS EXT
  0= IF POSTPONE [ELSE] THEN
; IMMEDIATE

: [THEN] \ 94 TOOLS EXT
; IMMEDIATE

\ Ruvim Pinka additions:

: [DEFINED] ( -- f ) \ "name"
  NextWord  SFIND  IF DROP TRUE ELSE 2DROP FALSE THEN
; IMMEDIATE

: [UNDEFINED]  ( -- f ) \ "name"
  POSTPONE [DEFINED] 0=
; IMMEDIATE

