\ words autoinclusion
\ require.cfg is the configuration file

: IN_T? ( waddr wu -- f ) \ is this word in the TARGET wordlist?
  ALSO TARGET CONTEXT @ SEARCH-WORDLIST PREVIOUS
  IF DROP TRUE
  ELSE FALSE THEN
;

: DEFINED_T? ( "word" -- ? ) \ is the next word in the TARGET wordlist?
  PARSE-NAME IN_T?
;

: REQUIRED_T ( waddr wu laddr lu -- )   \ include the file ladd:lu, if the word waddr:wu is not in TARGET
  2SWAP IN_T?
  IF
    2DROP
  ELSE
    INCLUDED
  THEN
;

: REQUIRE_T ( "word" "libpath" -- )   \ include the file "libpath", if there is no "word" in TARGET
  PARSE-NAME PARSE-NAME 2DUP + 0 SWAP C!
  REQUIRED_T
;

: IS_DELIMITER? ( c -- f )    \ c - is a delimiter? (space, tab or nul)
  DUP BL =
  OVER 0x9 = OR
  OVER 0 = OR
  NIP
;

0 VALUE POS                   \ position in the current line
CREATE BUF 128 CHARS ALLOT    \ buffer for the line of a file

: SKIP_BEGIN   ( -- )     \ skip starting delimiters
  BEGIN
    POS C@ IS_DELIMITER?
  WHILE
    POS 1+ TO POS
  REPEAT
;

: SKIP_END   ( -- )       \ skip to the first delimiter
  BEGIN
    POS C@ IS_DELIMITER? INVERT
  WHILE
    POS 1+ TO POS
  REPEAT
;

: WRD  ( -- waddr wu )      \ get the token waddr:wu
  SKIP_BEGIN POS SKIP_END POS OVER -
;

0 VALUE FOUND
: SEARCH_FILENAME ( waddr wu -- faddr fu | 0 )
\ search require.cfg file:
\ get the filename faddr fu, that contains the definition of the word waddr wu
\ 0 - not found
  S" src/require.cfg" FIND-FULLNAME R/O OPEN-FILE THROW
  >R
  0 TO FOUND
  BEGIN                 ( WADDR WU )
    BUF 64 R@ READ-LINE  THROW ( WADDR WU len not-eof-flag )
    0= >R
    ?DUP IF       ( WADDR WU LEN )  \ non empty line
      BUF TO POS
      BUF + 0!              \ limit from the end
      WRD       ( WADDR WU NADDR NU ) \ word name
      2OVER COMPARE 0= IF       \ found?
      2DROP WRD TRUE TO FOUND ( FADDR FU ) \ file name
      THEN
    THEN
  R> FOUND OR UNTIL
  R> CLOSE-FILE THROW
  FOUND INVERT IF 2DROP 0 THEN
;

: (TREQUIRED) ( addr u -- f ) \ include file for the word addr:u, return TRUE if found in the configuration file
  SEARCH_FILENAME ?DUP IF
    INCLUDED TRUE
  ELSE
    FALSE
  THEN
;

: TREQUIRED      ( waddr wu -- ) \ if the word is not in TARGET, include its file
  2DUP IN_T? IF
      2DROP
  ELSE
    (TREQUIRED) INVERT IF
      ABORT" not found ..."
    THEN
  THEN
;

: TREQUIRE   ( "word" -- ) \ if the "word" is not in TARGET, include its file
  PARSE-NAME TREQUIRED
;
