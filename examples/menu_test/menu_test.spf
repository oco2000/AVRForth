\ ATmega8
\ +DEBUG3

[F] DECIMAL [P]

SLOWLITS

8000000 == F_CPU

REQUIRE {SET}     lib/ports.spf
REQUIRE BUS.WRITE       lib/bus.spf

PD7 WIRE LED

PB0 WIRE BTN_SELECT
PB1 WIRE BTN_UP
PB2 WIRE BTN_DOWN
PB3 WIRE BTN_EXIT

GROUP{ BTN_SELECT BTN_UP BTN_DOWN BTN_EXIT }GROUP BUTTONS

0x4 == ChangesPerClick \ should be a power of two
BUS{ PB7 PB6 }BUS EncBus

REQUIRE D3               lib/lcd/lcd_conf_std.spf
REQUIRE LCD_CLRSCR       lib/lcd/lcd.spf
REQUIRE PROGRESSBAR      lib/lcd/progress.spf
REQUIRE RTOS_INIT_TIMER  lib/eertos/atmega8-eertos-config.spf
REQUIRE RTOS_RUN         lib/eertos/eertos.spf
REQUIRE BUTTONS_PERIOD   lib/buttons/buttons-std-config.spf
REQUIRE DEBOUNCE         lib/buttons/buttons.spf
REQUIRE MENU             lib/lcd_menu/menu.spf
REQUIRE INT-EDITOR       lib/lcd_menu/int_editor.spf
REQUIRE PROGRESS-DISPLAY lib/lcd_menu/progress_editor.spf
REQUIRE SUPERSTRING      lib/superstring.spf
REQUIRE DS1307           lib/ds1307.spf
REQUIRE EncInit          lib/encoder.spf

CVARIABLE DS1307_Present?
C" DS1307 not found!" == "DS1307_error"

[F] DECIMAL [P]

: BCD> ( bcd -- value )
  NIBBLE-SPLIT 0x0A * +
;

: >BCD  ( value -- bcd )
  0x0A /MOD ( hi lo )
  NIBBLE-JOIN
;

: LED_OFF
  LED {SET}
;

: BLIP
    LED {CLEAR}
    ['] LED_OFF 100 RTOS_SET_TIMER_TASK
;

: NOOP ;

: (H) ( n -- addr u )  0 <# # # # # #> ;
: (2D) ( n -- a u ) 0 <# # # #> ;
: 2D. ( n -- )  (2D) TYPE ;
: HEX.  ( n -- ) (H) TYPE SPACE ;

S" ./menu_config.spf" INCLUDED

: IDLE-TASK
  4 1 AT
  TRUE FORMAT-TIME TYPE

  2 0 AT
  FORMAT-SHORT-DATE TYPE

  12 0 AT
  GET-DOW (.DOW) TYPE

  ['] IDLE-TASK 250 RTOS_SET_TIMER_TASK
;

: UP   UP-MENU BLIP ;
: DOWN   DOWN-MENU BLIP ;
: ENTER   ENTER-MENU BLIP ;
: CANCEL   EXIT-MENU BLIP ;

TCREATE MENU_BTN_TASKS
' DOWN T,   ' UP T,   ' ENTER T,   ' CANCEL T,

: EnterMainMenu
  ['] IDLE-TASK RTOS_REMOVE_TASK
  M_Main SET-MENU
  ENTER-MENU
  MENU_BTN_TASKS BUTTONS_TASKS !
;

[VECT] ENTER-VOLUME

TCREATE IDLE_BTN_TASKS
' ENTER-VOLUME T,   ' ENTER-VOLUME T,   ' EnterMainMenu T,   ' NOOP T,

: SetIdleTask
  ON-EDITOR-CANCEL OFF
  EDITOR-TIMERED OFF
  IDLE_BTN_TASKS BUTTONS_TASKS !
  PAGE IDLE-TASK
;
' SetIdleTask M_Main SET-CANCEL

: (ENTER-VOLUME) ( -- )
  ['] IDLE-TASK RTOS_REMOVE_TASK
  ['] SetIdleTask ON-EDITOR-CANCEL !
  EDITOR-TIMERED ON
  VOLUME-EDITOR DROP
;
' (ENTER-VOLUME) [->] ENTER-VOLUME

: INIT
  DECIMAL
  LED {OUTPUT}  LED_OFF
  LCD_DISP_ON  LCD_INIT
  MENU-INIT
  PROGRESS-INIT
  EncInit
  DS1307_INIT 0= DS1307_Present? C!

  BUTTONS {GROUP.MASK} BUTTONS_ENABLED C!
  0x3 BUTTONS_REPEATABLE C!
  BUTTONS_INIT
  RTOS_INIT
  ['] BUTTONS_TASK RTOS_SET_TASK
  SetIdleTask
;

: ProcessEncoder ( increment -- )
  BUTTONS_TASKS @
  SWAP 0< IF CELL+ THEN
  @P RTOS_SET_TASK
;

: CheckEncoder
  EncIncrement ?DUP IF
    ProcessEncoder
  THEN
;

: MAIN
  INIT

  DS1307_Present? C@ 0= IF
    PAGE "DS1307_error" COUNTP TYPEP
  THEN

  RTOS_RUN

  BEGIN
    RTOS_TASK_MANAGER
  AGAIN
;

INT: TIMER
  CheckEncoder
  RTOS_TIMER_SERVICE
;INT

TIMER RTOS_TIMER_INTERRUPT INT!
