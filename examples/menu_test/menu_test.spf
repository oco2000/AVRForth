\ ATmega8
\ +DEBUG3

[F] DECIMAL [P]

SLOWLITS

TREQUIRE (.)
TREQUIRE C!E

1000000 == F_CPU

REQUIRE {SET}     lib/ports.spf

PD7 WIRE LED

PB0 WIRE BTN_SELECT
PB1 WIRE BTN_UP
PB2 WIRE BTN_DOWN
PB3 WIRE BTN_EXIT

GROUP{ BTN_SELECT BTN_UP BTN_DOWN BTN_EXIT }GROUP BUTTONS

REQUIRE D3              lib/lcd/lcd_conf_std.spf
REQUIRE LCD_CLRSCR      lib/lcd/lcd.spf
REQUIRE PROGRESSBAR     lib/lcd/progress.spf
REQUIRE RTOS_RUN        lib/eertos.spf
REQUIRE DEBOUNCE        lib/buttons.spf
REQUIRE MENU            lib/lcd_menu/menu.spf
REQUIRE INT-EDITOR      lib/lcd_menu/int_editor.spf
REQUIRE PROGRESS-DISPLAY lib/lcd_menu/progress_editor.spf
REQUIRE SUPERSTRING     lib/superstring.spf
REQUIRE DS1307          lib/ds1307.spf

CVARIABLE DS1307_Present?
C" DS1307 not found!" == "DS1307_error"

[F] DECIMAL [P]

: BCD> ( bcd -- value )
  NIBBLE-SPLIT 0x0A * +
;

: >BCD  ( value -- bcd )
  0x0A /MOD ( hi lo )
  NIBBLE-JOIN
;

: LED_OFF
  LED {SET}
;

: BLIP
    LED {CLEAR}
    ['] LED_OFF 100 RTOS_SET_TIMER_TASK
;

: UP   UP-MENU BLIP ;
: DOWN   DOWN-MENU BLIP ;
: ENTER   ENTER-MENU BLIP ;
: CANCEL   EXIT-MENU BLIP ;

TCREATE BTN_TASKS
' DOWN T,   ' UP T,   ' ENTER T,   ' CANCEL T,

: INIT
  DECIMAL
  LED {OUTPUT}
  LCD_DISP_ON  LCD_INIT
  MENU-INIT
  PROGRESS-INIT
  DS1307_INIT INVERT DS1307_Present? C!

  BUTTONS {GROUP.MASK} BUTTONS_ENABLED C!
  0x3 BUTTONS_REPEATABLE C!
  BTN_TASKS BUTTONS_TASKS !
  BUTTONS_INIT
  RTOS_INIT
  ['] BUTTONS_TASK RTOS_SET_TASK
;

: (H) ( n -- addr u )  0 <# # # # # #> ;
: (2D) ( n -- a u ) 0 <# # # #> ;
: HEX.  ( n -- ) (H) TYPE SPACE ;

\ ======================== VOLUME ==========================
CEVARIABLE VOLUME  0x0 VOLUME C!E
 63 == MAX-VOLUME
0x0 == MIN-VOLUME
: GET-VOLUME   ( -- value )   VOLUME C@E ;
: SET-VOLUME   ( value -- )   MIN-VOLUME MAX-VOLUME CLIP   VOLUME C!E ;

C" Volume" == I_VOLUME_Tttle
I_VOLUME_Tttle MIN-VOLUME MAX-VOLUME ' GET-VOLUME ' SET-VOLUME INT-EDITOR   VOLUME-EDITOR
' PROGRESS-DISPLAY   VOLUME-EDITOR SET-DISPLAY

I_VOLUME_Tttle MENU-ITEM I_Volume
' GET-VOLUME    I_Volume SET-GET-VALUE
' (.)           I_Volume SET-FORMAT
' VOLUME-EDITOR I_Volume SET-EDIT

\ ======================== MUTE ==========================
CEVARIABLE MUTED
: GET-MUTED   ( -- value )   MUTED C@E ;
: SET-MUTED   MUTED C!E ;
: TOGGLE-MUTE ( -- )
  GET-MUTED 0= SET-MUTED TRUE ( redraw ) ;

C" Mute" MENU-ITEM I_Mute
' GET-MUTED    I_Mute SET-GET-VALUE
' FORMAT-BOOL  I_Mute SET-FORMAT
' TOGGLE-MUTE  I_Mute SET-EDIT

\ ======================== BASS ==========================
CEVARIABLE BASS  0x0 BASS C!E
 31 == MAX-BASS
0x0 == MIN-BASS
: GET-BASS   ( -- value )   BASS C@E ;
: SET-BASS   ( value -- )   MIN-BASS MAX-BASS CLIP   BASS C!E ;

C" Bass" == I_BASS_Title
I_BASS_Title MIN-BASS MAX-BASS ' GET-BASS ' SET-BASS INT-EDITOR   BASS-EDITOR
' PROGRESS-DISPLAY  BASS-EDITOR SET-DISPLAY
TRUE                BASS-EDITOR SET-IMMEDIATE

I_BASS_Title MENU-ITEM I_Bass
' GET-BASS      I_Bass SET-GET-VALUE
' (.)           I_Bass SET-FORMAT
' BASS-EDITOR   I_Bass SET-EDIT

\ ======================== Treble ==========================
CEVARIABLE TREBLE  0x0 TREBLE C!E
 31 == MAX-TREBLE
0x0 == MIN-TREBLE
: GET-TREBLE   ( -- value )   TREBLE C@E ;
: SET-TREBLE   ( value -- )   MIN-TREBLE MAX-TREBLE CLIP   TREBLE C!E ;

C" Treble" == I_TREBLE_Title
I_TREBLE_Title MIN-TREBLE MAX-TREBLE ' GET-TREBLE ' SET-TREBLE INT-EDITOR   TREBLE-EDITOR
' PROGRESS-DISPLAY  TREBLE-EDITOR SET-DISPLAY
TRUE                TREBLE-EDITOR SET-IMMEDIATE

I_TREBLE_Title MENU-ITEM I_Treble
' GET-TREBLE      I_Treble SET-GET-VALUE
' (.)             I_Treble SET-FORMAT
' TREBLE-EDITOR   I_Treble SET-EDIT

\ ======================== Balance ==========================
CEVARIABLE BALANCE  0x0 BALANCE C!E
 31 == MAX-BALANCE
-30 == MIN-BALANCE
: GET-BALANCE   ( -- value )   BALANCE C@E C>N ;
: SET-BALANCE   ( value -- )   MIN-BALANCE MAX-BALANCE CLIP   BALANCE C!E ;

C" Balance" == I_BALANCE_Title
I_BALANCE_Title MIN-BALANCE MAX-BALANCE ' GET-BALANCE ' SET-BALANCE INT-EDITOR   BALANCE-EDITOR
' PROGRESS-DISPLAY  BALANCE-EDITOR SET-DISPLAY
TRUE                BALANCE-EDITOR SET-IMMEDIATE

I_BALANCE_Title MENU-ITEM I_Balance
' GET-BALANCE      I_Balance SET-GET-VALUE
' (.)              I_Balance SET-FORMAT
' BALANCE-EDITOR   I_Balance SET-EDIT

\ ------------------------ Sound menu ----------------------
0x0 I_Bass I_Treble I_Mute I_Balance I_Volume C" Sound" MENU M_Sound

\ DS1307 Utils

: DS1307R 0 ;
: DS1307W 1 ;

: DS1307_Access ( value? r/w params -- value? )
  C@P SWAP ( value? reg r/w )
  IF \ write
    DS1307_Present? C@ IF
      SWAP >BCD SWAP DS1307_SET
    ELSE
      DROP
    THEN
  ELSE \ read
    DS1307_Present? C@ IF
      DS1307_GET BCD>
    ELSE
      0
    THEN
  THEN
;

:: DS1307_reg
  PCREATE
    TC, TALIGN
  PDOES> ( value? r/w -- value? )
    DS1307_Access
  ;
;;

\ ======================== Hours ==========================
 23 == MAX-HOURS
 00 == MIN-HOURS
DS1307_Hours DS1307_reg _Hours
: GET-HOURS   ( -- value )   DS1307R _Hours ;
: SET-HOURS   ( value -- )   DS1307W _Hours ;

C" Hours" == I_HOURS_Title
I_HOURS_Title MIN-HOURS MAX-HOURS ' GET-HOURS ' SET-HOURS INT-EDITOR   HOURS-EDITOR
' (2D)  HOURS-EDITOR SET-FORMAT

I_HOURS_Title MENU-ITEM I_Hours
' GET-HOURS      I_Hours SET-GET-VALUE
' (2D)           I_Hours SET-FORMAT
' HOURS-EDITOR   I_Hours SET-EDIT

\ ======================== Minutes ==========================
 59 == MAX-MINUTES
 00 == MIN-MINUTES
DS1307_Minutes DS1307_reg _Minutes
: GET-MINUTES   ( -- value )   DS1307R _Minutes ;
: SET-MINUTES   ( value -- )   DS1307W _Minutes ;

C" Minutes" == I_MINUTES_Title
I_MINUTES_Title MIN-MINUTES MAX-MINUTES ' GET-MINUTES ' SET-MINUTES INT-EDITOR   MINUTES-EDITOR
' (2D)  MINUTES-EDITOR SET-FORMAT

I_MINUTES_Title MENU-ITEM I_Minutes
' GET-MINUTES      I_Minutes SET-GET-VALUE
' (2D)             I_Minutes SET-FORMAT
' MINUTES-EDITOR   I_Minutes SET-EDIT

\ ======================== Seconds ==========================
DS1307_Seconds DS1307_reg _SECONDS
: GET-SECONDS   ( -- value )   DS1307R _SECONDS ;
: SET-SECONDS   ( value -- )   DS1307W _SECONDS ;
: RESET-SECONDS   ( value -- )  0 SET-SECONDS ;

C" Seconds" == I_SECONDS_Title

I_SECONDS_Title MENU-ITEM I_Seconds
' GET-SECONDS     I_Seconds SET-GET-VALUE
' (2D)            I_Seconds SET-FORMAT
' RESET-SECONDS   I_Seconds SET-EDIT

\ ------------------------ Time menu ----------------------
: FORMAT-TIME ( -- a u )
  GET-MINUTES 0 <# # # [ CHAR : ] LITERAL HOLD 2DROP GET-HOURS 0 # # #>
;

0x0 I_Seconds I_Minutes I_Hours C" Time" MENU M_Time
' FORMAT-TIME            M_Time SET-FORMAT

\ ======================== Day ==========================
 31 == MAX-DAY
 01 == MIN-DAY
DS1307_Day DS1307_reg _Day
: GET-DAY   ( -- value )   DS1307R _Day ;
: SET-DAY   ( value -- )   DS1307W _Day ;

C" Day" == I_DAY_Title
I_DAY_Title MIN-DAY MAX-DAY ' GET-DAY ' SET-DAY INT-EDITOR   DAY-EDITOR
' (2D)  DAY-EDITOR SET-FORMAT

I_DAY_Title MENU-ITEM I_Day
' GET-DAY      I_Day SET-GET-VALUE
' (2D)         I_Day SET-FORMAT
' DAY-EDITOR   I_Day SET-EDIT

\ ======================== DayOfWeek ==========================
 07 == MAX-DOW
 01 == MIN-DOW
DS1307_Weekday DS1307_reg _DOW
: GET-DOW   ( -- value )   DS1307R _DOW ;
: SET-DOW   ( value -- )   DS1307W _DOW ;

C" SunMonTueWedThuFriSat" 0x3 SUPERSTRING "DOW"
: (.DOW) ( DOW -- a u )
  MIN-DOW MAX-DOW CLIP 1- "DOW"
;

C" Weekday" == I_DOW_Title
I_DOW_Title MIN-DOW MAX-DOW ' GET-DOW ' SET-DOW INT-EDITOR   DOW-EDITOR
' (.DOW)  DOW-EDITOR SET-FORMAT

I_DOW_Title MENU-ITEM I_DOW
' GET-DOW      I_DOW SET-GET-VALUE
' (.DOW)       I_DOW SET-FORMAT
' DOW-EDITOR   I_DOW SET-EDIT

\ ======================== Month ==========================
 12 == MAX-MONTH
 01 == MIN-MONTH
DS1307_Month DS1307_reg _Month
: GET-MONTH   ( -- value )   DS1307R _Month ;
: SET-MONTH   ( value -- )   DS1307W _Month ;

C" JanFebMarAprMayJunJulAugSepOctNovDec" 0x3 SUPERSTRING "Month"
: (.MONTH) ( month -- a u )
  MIN-MONTH MAX-MONTH CLIP 1- "Month"
;

C" Month" == I_MONTH_Title
I_MONTH_Title MIN-MONTH MAX-MONTH ' GET-MONTH ' SET-MONTH INT-EDITOR   MONTH-EDITOR
' (.MONTH)  MONTH-EDITOR SET-FORMAT

I_MONTH_Title MENU-ITEM I_Month
' GET-MONTH      I_Month SET-GET-VALUE
' (.MONTH)       I_Month SET-FORMAT
' MONTH-EDITOR   I_Month SET-EDIT

\ ======================== Year ==========================
 99 == MAX-YEAR
 00 == MIN-YEAR
DS1307_Year DS1307_reg _Year
: GET-YEAR   ( -- value )   DS1307R _Year ;
: SET-YEAR   ( value -- )   DS1307W _Year ;

C" Year" == I_YEAR_Title
I_YEAR_Title MIN-YEAR MAX-YEAR ' GET-YEAR ' SET-YEAR INT-EDITOR   YEAR-EDITOR

I_YEAR_Title MENU-ITEM I_Year
' GET-YEAR      I_Year SET-GET-VALUE
' (2D)          I_Year SET-FORMAT
' YEAR-EDITOR   I_Year SET-EDIT

\ ------------------------ Date menu ----------------------
: SLASH  ( -- )   [ CHAR / ] LITERAL HOLD ;
: 2D  ( 2x -- 2y )
  NIP NIP 0 # #
;

: FORMAT-SHORT-DATE ( -- a u )
  0 0 <#
    GET-YEAR 2D SLASH
    GET-MONTH 2D SLASH
    GET-DAY 2D
  #>
;

0x0 I_Year I_Month I_DOW I_Day C" Date" MENU M_Date
' FORMAT-SHORT-DATE            M_Date SET-FORMAT

\ ------------------------ Setup menu ----------------------
0x0 M_Time M_Date C" Setup" MENU M_Setup

\ ------------------------ Main menu ----------------------
0x0 M_Sound M_Setup C" _" MENU M_Main
M_Main SET-MAIN

: MAIN
  INIT

  DS1307_Present? C@ INVERT IF
    "DS1307_error" COUNTP TYPEP
  THEN

  M_Main SET-MENU ENTER-MENU

  RTOS_RUN

  BEGIN
      RTOS_TASK_MANAGER
  AGAIN
;

' MAIN [->] {MAIN}
