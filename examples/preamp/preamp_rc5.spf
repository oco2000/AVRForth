\ RC5 configuration
\ Proteus
\ ~0x1C == RC5Address
\ ~0x11 == RC5PowerBtn

\ Real HW
\ UET-606 AUX code 0552
0x14 == RC5Address

0x0C == RC5PowerBtn
0x0D == RC5MuteBtn
0x10 == RC5VolUpBtn
0x11 == RC5VolDownBtn
0x20 == RC5UpBtn
0x21 == RC5DownBtn
0x1E == RC5RightBtn
0x1F == RC5LeftBtn
0x29 == RC5OKBtn
0x3A == RC5ClearBtn
0x01 == RC51Btn
0x02 == RC52Btn
0x03 == RC53Btn

\\ create RC5 words that emulate buttons
:: EmulatedButton
  PCREATE T,
  PDOES> @P set_Nth_task ;
;;

\\ RC5 emulated buttons
0x4 EmulatedButton PowerToggle
0x0 EmulatedButton Down
0x1 EmulatedButton Up
0x2 EmulatedButton Enter
0x3 EmulatedButton Cancel
0x5 EmulatedButton Input1
0x6 EmulatedButton Input2
0x7 EmulatedButton Input3

\ Set MSB of task address if the button is repeatable
0x8000 == IsRepeatable
:: REPEATABLE ( addr - addr || 0x8000 )
  [F] IsRepeatable OR [P]
;;

TCREATE RC5Commands
\\ RC5 Commands table
\ Button               Task
RC5PowerBtn    TC,   ' PowerToggle      T,
RC5VolUpBtn    TC,   ' Up   REPEATABLE  T,
RC5VolDownBtn  TC,   ' Down REPEATABLE  T,
RC5UpBtn       TC,   ' Up               T,
RC5DownBtn     TC,   ' Down             T,
RC5RightBtn    TC,   ' Up   REPEATABLE  T,
RC5LeftBtn     TC,   ' Down REPEATABLE  T,
RC5MuteBtn     TC,   ' MuteToggle       T,
RC5OKBtn       TC,   ' Enter            T,
RC5ClearBtn    TC,   ' Cancel           T,
RC51Btn        TC,   ' Input1           T,
RC52Btn        TC,   ' Input2           T,
RC53Btn        TC,   ' Input3           T,
0 TC, TALIGN

CVARIABLE RC5Toggle \ current toggle byte

: SearchRC5Command ( command -- addr | 0 )
  >R RC5Commands BEGIN
    DUP C@P DUP R@ = IF ( addr com )
      DROP CHAR+ @P
      RDROP EXIT
    THEN
    SWAP CHAR+ CELL+ SWAP ( addr+3 com )
  0= UNTIL
  RDROP DROP 0
;

: ExecuteRC5Command ( addr -- )  \ Search the command and execute as a task if found
  ?DUP IF
    IsRepeatable INVERT AND RTOS_SET_TASK
  THEN
;

: HandleRC5Command ( command toggle -- )
  DUP RC5Toggle C@ =
  SWAP RC5Toggle C! ( command flag )
  >R SearchRC5Command R>
  IF \ repeated ( addr )
    DUP 0< IFNOT DROP EXIT THEN
  THEN
  ExecuteRC5Command
;

: RC5CommandReceived ( -- )
  RC5_Command 2@ RC5_Analyze IF
    \ address command toggle
    ROT RC5Address = IF HandleRC5Command ELSE 2DROP THEN
  THEN
;
