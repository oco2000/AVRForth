\ TDA7313 audio processor functions

@DECIMAL

REQUIRE I2C_START lib/i2c.spf
TREQUIRE C!E

0x88 == TDA7313 \ I2C Address

CEVARIABLE Snd.CurrentInput \ Current stereo input (1..3)
0x1 Snd.CurrentInput C!E

\ Input flags
0x0 BIT == Input.flag.muted
0x1 BIT == Input.flag.loudness

\ Input parameters
0x0
0x1  -- Input.volume
0x1  -- Input.balance
0x1  -- Input.savedBalance
0x1  -- Input.bass
0x1  -- Input.treble
0x1  -- Input.gain
0x1  -- Input.flags
== /InputParams

EHERE == Snd.InputsData \ Data for all inputs
[F] 0x3 /InputParams * [P] == /AllInputParams
/AllInputParams EALLOT

: Snd.sendOne ( data -- ) \  write 1 byte data
  [IFDEBUG2] DUP DEBUG. [THEN]

  TDA7313 I2C_START DROP
  I2C_WRITE_DROP
  I2C_STOP
;

: Snd.getInput ( -- input ) \ 1..3
  Snd.CurrentInput C@E
;

: Snd.currentInputData ( -- addr ) \ Parameters for current input
  Snd.getInput 1- /InputParams * Snd.InputsData +
;

\ flags

: Snd.getFlags ( -- b )
  Snd.currentInputData Input.flags C@E
;

: Snd.setFlags ( b -- )
  Snd.currentInputData Input.flags C!E
;

\ volume

: Snd.setVolume ( volume -- ) \ 0..63
  Snd.currentInputData Input.volume C!E
;

: Snd.getVolume ( -- volume ) \ 0..63
  Snd.currentInputData Input.volume C@E
;

: Snd.writeVolume ( -- ) \ 0..63
  Snd.getVolume
  0x3F SWAP-
  Snd.sendOne
;

\ balance

: Snd.setBalance ( balance -- ) \ -31..31
  Snd.currentInputData Input.balance C!E
;

: Snd.getBalance ( balance -- ) \ -31..31
  Snd.currentInputData Input.balance C@E C>S
;

: Snd.writeBalance ( -- ) \ -31..31
  Snd.getBalance
  0 SWAP DUP 0< IF
    ABS SWAP
  THEN
  0x80 OR Snd.sendOne \ Left
  0xA0 OR Snd.sendOne \ Right
;

\ mute

: Snd.muted? ( -- f )
  Snd.getFlags Input.flag.muted AND
;

: Snd.toggleMute ( -- )
  Snd.getFlags

  Snd.muted? IF
    Snd.currentInputData Input.savedBalance C@E
    Snd.currentInputData Input.balance C!E
  ELSE
    Snd.currentInputData Input.balance C@E
    Snd.currentInputData Input.savedBalance C!E
  THEN

  Input.flag.muted XOR
  Snd.setFlags
;

: Snd.mute ( -- )
  0x9F Snd.sendOne
  0xBF Snd.sendOne
  0xDF Snd.sendOne \ LR
  0xFF Snd.sendOne \ RR
;

: Snd.writeMuted ( -- )
  Snd.muted? IF
    Snd.mute
  ELSE
    Snd.writeBalance
  THEN
;

\ loudness

: Snd.loud? ( -- f )
  Snd.getFlags Input.flag.loudness AND
;

: Snd.toggleLoudness ( -- )
  Snd.getFlags
  Input.flag.loudness XOR
  Snd.setFlags
;

\ bass

: Snd.setBass ( bass -- ) \ -7..7
  Snd.currentInputData Input.bass C!E
;

: Snd.getBass ( bass -- ) \ -7..7
  Snd.currentInputData Input.bass C@E C>S
;

: Snd.getEqData ( bass/treble -- data )
  DUP ABS 7 SWAP-
  SWAP 0< INVERT IF 0x8 OR THEN
;

: Snd.writeBass ( -- )
  Snd.getBass Snd.getEqData
  0x60 OR Snd.sendOne
;

\ treble

: Snd.setTreble ( treble -- ) \ -7..7
  Snd.currentInputData Input.treble C!E
;

: Snd.getTreble ( -- treble ) \ -7..7
  Snd.currentInputData Input.treble C@E C>S
;

: Snd.writeTreble ( -- )
  Snd.getTreble Snd.getEqData
  0x70 OR Snd.sendOne
;

\ gain

: Snd.setGain ( gain -- ) \ 0..3
  Snd.currentInputData Input.gain C!E
;

: Snd.getGain ( -- gain ) \ 0..3
  Snd.currentInputData Input.gain C@E
;

\ input switching

: Snd.writeAudioSwitch ( -- )
  Snd.getGain  3 SWAP- 3 LSHIFT
  Snd.loud? 0= 1 AND   2 LSHIFT  OR
  Snd.getInput 1-   OR
  0x40 OR Snd.sendOne
;

: Snd.setInput ( n -- ) \ 1..3
  Snd.CurrentInput C!E
;

: Snd.writeInput ( -- )
  Snd.writeAudioSwitch
  Snd.writeTreble
  Snd.writeBass
  Snd.writeMuted
  Snd.writeVolume
;

\ set default values for all inputs and set 1st input

: Snd.setDefault
  Snd.InputsData /AllInputParams BEGIN
    1- >R
    0 OVER C!E
    1+
  R> [UNTIL<>0]
  DROP 1 Snd.setInput
;

\ initialize and write stored data to the chip

: Snd.Init
  Snd.getInput 1 4 WITHIN INVERT IF
    \ EEPROM is not initialized
    Snd.setDefault
  THEN
  Snd.writeInput
;

BASE!
