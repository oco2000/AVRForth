\ ATmega8
 +DEBUG2

[F] DECIMAL [P]

SLOWLITS

8000000 == F_CPU

0x0 == MODE_SUSPEND
0x1 == MODE_ON
0x2 == MODE_MENU

REQUIRE {SET}           lib/ports.spf
REQUIRE BUS.WRITE       lib/bus.spf

TREQUIRE NOOP

PB0 WIRE BTN_DOWN
PB1 WIRE BTN_UP
PB2 WIRE BTN_SELECT
PB3 WIRE BTN_EXIT
PB4 WIRE BTN_POWER

BUS{ BTN_POWER BTN_EXIT BTN_SELECT BTN_UP BTN_DOWN }BUS BUTTONS
0x3 == REPEATABLE_MASK
0x1F == ENABLED_MASK

0x4 == ChangesPerClick \ should be a power of two
BUS{ PD1 PD0 }BUS EncBus

:: LCD_I2C ;;
0x4E == LCD_Addr
REQUIRE LCD_CLRSCR       lib/lcd/lcd.spf
REQUIRE LCD_CHAR         lib/lcd/definechar.spf
\ REQUIRE BIG_SETUP        lib/lcd/big.spf
REQUIRE PROGRESSBAR      lib/lcd/progress.spf
REQUIRE RTOS_INIT_TIMER  lib/eertos/atmega8-eertos-config.spf
REQUIRE RTOS_RUN         lib/eertos/eertos.spf
REQUIRE BUTTONS_PERIOD   lib/buttons/buttons-std-config.spf
REQUIRE DEBOUNCE         lib/buttons/buttons.spf
REQUIRE MENU             lib/lcd_menu/menu.spf
REQUIRE INT-EDITOR       lib/lcd_menu/int_editor.spf
REQUIRE PROGRESS-DISPLAY lib/lcd_menu/progress_editor.spf
REQUIRE SUPERSTRING      lib/superstring.spf
REQUIRE DS1307           lib/ds1307.spf
REQUIRE EncInit          lib/encoder.spf
REQUIRE RC5_Init_Timer   lib/rc5/atmega8-rc5-config.spf
REQUIRE RC5Init          lib/rc5/rc5.spf
REQUIRE Snd.Init         tda7313.spf

CVARIABLE DS1307_Present?
C" DS1307 not found!" == "DS1307_error"

[F] DECIMAL [P]

: BCD> ( bcd -- value )
  NIBBLE-SPLIT 0x0A * +
;

: >BCD  ( value -- bcd )
  0x0A /MOD ( hi lo )
  NIBBLE-JOIN
;

: (H) ( n -- addr u )  0 <# # # # # #> ;
: (2D) ( n -- a u ) 0 <# # # #> ;
: 2D. ( n -- )  (2D) TYPE ;
: HEX.  ( n -- ) (H) TYPE SPACE ;

S" ./menu_config.spf" INCLUDED

: RC5_Command ( -- )
  RC5_Command 2@ RC5_Analyze IF
    2DROP DROP
  THEN
;

: IDLE-TASK
  [IFDEBUG2] EXIT [THEN]
  4 1 AT
  TRUE FORMAT-TIME TYPE

  2 0 AT
  FORMAT-SHORT-DATE TYPE

  12 0 AT
  GET-DOW (.DOW) TYPE

  ['] IDLE-TASK 250 RTOS_SET_TIMER_TASK
;

TCREATE MENU_BTN_TASKS
' DOWN-MENU T,   ' UP-MENU T,   ' ENTER-MENU T,   ' EXIT-MENU T,   ' NOOP T,

: EnterMainMenu
  ['] IDLE-TASK RTOS_REMOVE_TASK
  M_Main SET-MENU
  ENTER-MENU
  MENU_BTN_TASKS BUTTONS_TASKS !
;

[VECT] ENTER-VOLUME

TCREATE IDLE_BTN_TASKS
' ENTER-VOLUME T,   ' ENTER-VOLUME T,   ' EnterMainMenu T,   ' NOOP T,   ' NOOP T,

: SetIdleTask
  ON-EDITOR-CANCEL OFF
  EDITOR-TIMERED OFF
  IDLE_BTN_TASKS BUTTONS_TASKS !
  PAGE IDLE-TASK
;
' SetIdleTask M_Main SET-CANCEL

: (ENTER-VOLUME) ( -- )
  ['] IDLE-TASK RTOS_REMOVE_TASK
  ['] SetIdleTask ON-EDITOR-CANCEL !
  EDITOR-TIMERED ON
  VOLUME-EDITOR DROP
;
' (ENTER-VOLUME) [->] ENTER-VOLUME

: INIT
  DECIMAL
  LCD_DISP_ON  LCD_INIT
  MENU-INIT
  PROGRESS-INIT
  EncInit
  DS1307_INIT 0= DS1307_Present? C!
  RC5_Init
  Snd.Init

  ENABLED_MASK BUTTONS_ENABLED C!
  REPEATABLE_MASK BUTTONS_REPEATABLE C!
  BUTTONS_INIT
  RTOS_INIT
  ['] BUTTONS_TASK RTOS_SET_TASK
  ['] RC5_Command IS RC5_Task
  SetIdleTask
;

: ProcessEncoder ( increment -- )
  BUTTONS_TASKS @
  SWAP 0< IF CELL+ THEN
  @P RTOS_SET_TASK
;

: CheckEncoder
  EncIncrement ?DUP IF
    ProcessEncoder
  THEN
;

: MAIN
  INIT

  DS1307_Present? C@ 0= IF
    PAGE "DS1307_error" COUNTP TYPEP
  THEN

  RTOS_RUN

  BEGIN
    RTOS_TASK_MANAGER
  AGAIN
;

INT: TIMER
  CheckEncoder
  RTOS_TIMER_SERVICE
;INT

TIMER RTOS_TIMER_INTERRUPT INT!
