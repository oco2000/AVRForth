\ ATmega8
\ +DEBUG3

@DECIMAL

16000000 == F_CPU

REQUIRE _USART_INIT   lib/usart/atmegax8-usart-config.spf
REQUIRE USART         lib/usart/usart.spf
REQUIRE I2C_START     lib/i2c.spf
REQUIRE DS1307        lib/ds1307.spf
REQUIRE MS            lib/delay.spf

: .COLON  ( -- )   [ CHAR : ] LITERAL EMIT ;

: ## ( n -- a u )
  0 <# # # #>
;

: SHOW-TIME   ( -- )
  DS1307_GET_TIME ## TYPE .COLON ## TYPE .COLON ## TYPE CR
;

: MAIN
  HEX \ RTC digits are BCD, so we can use HEX to display them
  9600 BAUD USART_INIT
  I2C_INIT
  DS1307_INIT 0= IF
    BEGIN
      SHOW-TIME
      1000 MS
    AGAIN
  ELSE
    ." No RTC detected"
  THEN
;

BASE!
