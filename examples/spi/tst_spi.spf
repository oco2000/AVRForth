\ 74HC595 shift registers via SPI
\ for ATMega168
\ see spi_test.dsn

[F] DECIMAL [P]

REQUIRE {WRITE} lib/ports.spf

8000000 == F_CPU

PC3 WIRE CLK   \ clock
PD1 WIRE LED

PB2 WIRE SS
PB3 WIRE MOSI
PB4 WIRE MISO
PB5 WIRE SCK

GROUP{ MOSI SCK SS }GROUP SPI_OUT

REQUIRE MS     lib/delay.spf

:: LED_TOGGLE
  LED {TOGGLE}
;;

: SPI-INIT      \ init SPI in Master mode
  \ MOSI, SCK and SS are outputs
  SPI_OUT {OUTPUT}
  \ Turn on SPI, Master, set clock rate fck/4
  BITS{ SPE MSTR }BITS  SPCR C!
;

: CLK_TICK      \ strobe
  CLK {SET}
  CLK {CLEAR}
;

: SPI-TRANSMIT ( c -- ) \ write 1 byte to the registers
  \ start
  SPDR C!
  \ wait for ready
  BEGIN
     SPSR C@
     SPIF BIT AND
  UNTIL \ until SPIF is not set
;

: LEDS ( word -- )  \ write a word to the registers
  BYTE-SPLIT   SPI-TRANSMIT   SPI-TRANSMIT
  CLK_TICK
;

: INIT
  CLK {OUTPUT}
  LED {OUTPUT}
  LED {CLEAR}   \  turn off the LED
  SPI-INIT
;

: MAIN
  INIT
  0x8000
  BEGIN
      2* DUP 0= IF    \ left shift
        DROP 1
      THEN
      LED_TOGGLE
      DUP LEDS        \ output
      150 MS          \ delay
  AGAIN
;
