\ тест для обработчика кнопок

[F] DECIMAL [P]
\ +DEBUG

8000000 == F_CPU

TREQUIRE !E

REQUIRE {SET}     lib/ports.spf

\ определения портов
PD7 WIRE LED

\ обязательно надо определить порт с кнопками для buttons.spf
PB0 WIRE BTN_SET              \  сами кнопки
PB1 WIRE BTN_+
PB2 WIRE BTN_-

GROUP{ BTN_SET BTN_+ BTN_- }GROUP BUTTONS

REQUIRE RTOS_INIT   lib/eertos.spf
REQUIRE DEBOUNCE    lib/buttons.spf

MACRO: LED_ON
    LED {CLEAR}
;MACRO

EVARIABLE PERIOD   100 PERIOD !E

: LED_OFF   \ нельзя сделать макросом, потому что это слово
            \ ставится в очередь как задача
    LED {SET}
;

: BLINK         \ мигнуть светодиодом на 100 мс
    LED_ON
    ['] LED_OFF PERIOD @E RTOS_SET_TIMER_TASK
;

: +!E ( n addr -- )
    DUP @E ROT + SWAP !E
;

: BTN0_TASK
    100 PERIOD +!E   BLINK
;

: BTN1_TASK
    PERIOD @E 100 - 100 MAX PERIOD !E   BLINK
;

: BTN2_TASK
    BLINK
;

TCREATE BTN_TASKS           \ задачи для кнопок
' BTN0_TASK T,   ' BTN1_TASK T,   ' BTN2_TASK T,

: INIT
    LED {OUTPUT}
    PERIOD @E 0= IF 200 PERIOD !E THEN

    BUTTONS {GROUP.MASK} BUTTONS_ENABLED C!                      \ все кнопки вкл
    [ BUTTONS BTN_SET -GROUP {GROUP.MASK} ] LITERAL  \ эта кнопка не повторяет нажатия
      BUTTONS_REPEATABLE C!
    BTN_TASKS BUTTONS_TASKS !                       \ задачи для кнопок
    BUTTONS_INIT                                    \ инициализация порта

    RTOS_INIT
    ['] BUTTONS_TASK RTOS_SET_TASK                  \ обработчик кнопок
    RTOS_RUN
    BLINK
;

: MAIN
    INIT
    BEGIN
        RTOS_TASK_MANAGER
    AGAIN
;

' MAIN [->] {MAIN}

