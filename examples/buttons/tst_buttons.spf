\ тест для обработчика кнопок

[F] DECIMAL [P]
\ +DEBUG

8000000 == F_CPU

TREQUIRE !E

REQUIRE {SET}           lib/ports.spf
REQUIRE BUS.WRITE       lib/bus.spf

\ wire definitions
PD7 WIRE LED

\ buttons
PB0 WIRE BTN_SET
PB1 WIRE BTN_+
PB2 WIRE BTN_-

BUS{ BTN_- BTN_+ BTN_SET }BUS BUTTONS

REQUIRE RTOS_INIT_TIMER   lib/eertos/atmegax8-eertos-config.spf
REQUIRE RTOS_INIT         lib/eertos/eertos.spf
REQUIRE BUTTONS_PERIOD    lib/buttons/buttons-std-config.spf
REQUIRE DEBOUNCE          lib/buttons/buttons.spf

MACRO: LED_ON
  LED {CLEAR}
;MACRO

EVARIABLE PERIOD   100 PERIOD !E

: LED_OFF   \ can't be a macro
            \ this word is a task
  LED {SET}
;

: BLINK         \ blink for PERIOD msecs
  LED_ON
  ['] LED_OFF PERIOD @E RTOS_SET_TIMER_TASK
;

: +!E ( n addr -- )
  DUP @E ROT + SWAP !E
;

: BTN0_TASK \ increment the period
  100 PERIOD +!E   BLINK
;

: BTN1_TASK \ decrement the period
  PERIOD @E 100 - 100 MAX PERIOD !E   BLINK
;

: BTN2_TASK \ blink
  BLINK
;

TCREATE BTN_TASKS           \ button tasks
' BTN0_TASK T,   ' BTN1_TASK T,   ' BTN2_TASK T,

: INIT
  LED {OUTPUT}
  PERIOD @E 0= IF 200 PERIOD !E THEN

  0x7 BUTTONS_ENABLED C!                          \ all buttons enabled
  0x6 BUTTONS_REPEATABLE C!
  BTN_TASKS BUTTONS_TASKS !                       \ set tasks
  BUTTONS_INIT

  RTOS_INIT
  ['] BUTTONS_TASK RTOS_SET_TASK                  \ buttons handler
  RTOS_RUN
  BLINK
;

: MAIN
  INIT
  BEGIN
    RTOS_TASK_MANAGER
  AGAIN
;
