\ USART library
TREQUIRE EMIT

:: BAUD ( baudrate -- UBRR ) \ преобразовать скорость в константу, только для компиляции
[F]
  QLIT1? IF
    [[ F_CPU 0x10 / ]] LITERAL SWAP / 1-
    (LIT)
  ELSE
    E_LITERAL
  THEN
[P]
;;

: USART_SEND ( c -- )
[
\ wait for empty transmit buffer
  UDRE UCSR0A sbis
  ' USART_SEND rjmp
\ Put data into buffer, sends the data
  tosl UDR0 _out
  popt
]
;

: USART ( -- )
    ['] USART_SEND IS EMIT
;

[F] DEVICE mega8 = [P] [IF] \

    : USART_INIT ( UBRR -- )
      [ tosh UBRR0H _out
        tosl UBRR0L _out
        popt
        NEW
      ]
      TXEN BIT UCSR0B C!    \ enable transmitter
      BITS{ URSEL UCSZ1 UCSZ0 }BITS UCSR0C C! \ 8bit 1stop
      USART
    ;

[ELSE]

    : USART_INIT ( UBRR -- )
      [ tosh UBRR0H _out
        tosl UBRR0L _out
        popt
        NEW
      ]
      TXEN BIT UCSR0B C!    \ enable transmitter
      BITS{ UCSZ1 UCSZ0 }BITS UCSR0C C! \ 8bit 1stop
      USART
    ;

[THEN]

[F] DEVICE mega128 = [P] [IF] \ 2 usarts

    : USART1_SEND ( c -- )
    [
    \ wait for empty transmit buffer
      begin
        UCSR1A zl _in
        UDRE BIT zl andi
      until_=0
    \ Put data into buffer, sends the data
      tosl UDR1 _out
      popt
    ]
    ;

    : USART1 ( -- )
      ['] USART1_SEND IS EMIT
    ;


    : USART1_INIT ( baudrate -- )
      [ tosh UBRR1H _out
        tosl UBRR1L _out
        popt
        NEW
      ]
      TXEN BIT UCSR1B C!    \ enable transmitter
      BITS{ UCSZ1 UCSZ0 }BITS UCSR1C C! \ 8bit 1stop
      USART1
    ;

[ELSE] \ 1 usart


[THEN]

