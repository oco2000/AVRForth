\ DS1307 RTC routines

0xD0 == DS1307                  \  Device Address for DS1307  (11010000)

\  registers
0x0 ==  DS1307_Seconds
    0x7 ==  DS1307_CH              \  Clock halt bit
0x1 ==  DS1307_Minutes
0x2 ==  DS1307_Hours
    0x6 ==  DS1307_Hour_mode   \  12/24 hours mode bit
0x3 ==  DS1307_Weekday
0x4 ==  DS1307_Day
0x5 ==  DS1307_Month
0x6 ==  DS1307_Year
0x7 ==  DS1307_Ctrl_reg
    0x7 ==  DS1307_OUT             \  OUT bit
    0x5 ==  DS1307_SQWE            \  SQWE bit
    0x1 ==  DS1307_RS1             \  RS1 bit
    0x0 ==  DS1307_RS0             \  RS0 bit

REQUIRE I2C_START lib/i2c.spf

: _DS1307_start_write ( -- err )
    [ DS1307 #I2C_WRITE [F] + [P] ] LITERAL I2C_START
;

: _DS1307_start_read ( -- err )
    [ DS1307 #I2C_READ [F] + [P] ] LITERAL I2C_START
;

: DS1307_GET ( reg -- value ) \  read a register
    _DS1307_start_write DROP
    I2C_WRITE_DROP      \ write register
    I2C_STOP
    _DS1307_start_read DROP
    I2C_READ_NAK    \ read value
    I2C_STOP
;

: DS1307_SET ( value reg -- ) \  write a register
    _DS1307_start_write DROP
    I2C_WRITE_DROP       \ write register
    I2C_WRITE_DROP      \ write value
    I2C_STOP
;

: DS1307_INIT ( -- 0 - ds1307 present | -1 - error )
    _DS1307_start_write
    DUP INVERT IF   \ device present
        I2C_STOP
        0 DS1307_GET
        [ DS1307_CH BIT [F] INVERT [P] ] LITERAL AND \ clock halt bit reset - turn on the clock
        0 DS1307_SET
    THEN
;

: _DS1307_GET_TRIPLE ( r -- [r] [r+1] [r+2] ) \  read three registers
    _DS1307_start_write DROP
    I2C_WRITE_DROP
    I2C_STOP
    _DS1307_start_read DROP
    I2C_READ_ACK
    I2C_READ_ACK
    I2C_READ_NAK
    I2C_STOP
;


: DS1307_GET_TIME ( -- s m h ) \  read time
    DS1307_Seconds _DS1307_GET_TRIPLE
;

: _DS1307_SET_TRIPLE ( [r+2] [r+1] [r] r -- ) \  write three registers
    _DS1307_start_write DROP
    I2C_WRITE_DROP
    I2C_WRITE_DROP
    I2C_WRITE_DROP
    I2C_WRITE_DROP
    I2C_STOP
;

: DS1307_SET_TIME ( h m s -- ) \  write time
    DS1307_Seconds _DS1307_SET_TRIPLE
;

: DS1307_GET_DATE ( -- d m y ) \  read date
    DS1307_Day _DS1307_GET_TRIPLE
;

: DS1307_SET_DATE ( y m d -- ) \  write date
    DS1307_Day _DS1307_SET_TRIPLE
;
