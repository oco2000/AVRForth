\ USART library
TREQUIRE EMIT

:: BAUD ( baudrate -- UBRR ) \ convert speed to a constant, for compilation only
[F]
  QLIT1? IF
    [[ F_CPU 0x10 / ]] LITERAL SWAP / 1-
    (LIT)
  ELSE
    E_LITERAL
  THEN
[P]
;;

: USART_SEND ( c -- )
[
  \ wait for empty transmit buffer
  begin
    UCSR0A zl _in
    UDRE BIT zl andi
  until_=0
  \ Put data into buffer, sends the data
  tosl UDR0 _out
  popt
]
;

: ?USART_RECEIVED ( -- f )
[
  pusht
  UCSR0A zl _in
  RXC BIT zl andi
  0x0 tosh ldi
  if_<>0
    tosh dec
  then
  tosh tosl mov
]
;

: USART_RECEIVE ( -- b ) \ read data from receive buffer, need to check with ?USART_RECEIVED first
  [
    pusht
    UDR0 tosl _in
    0x0 tosh ldi
  ]
;

: USART ( -- )
  ['] USART_SEND IS EMIT
;

: USART_INIT ( UBRR -- )
  [ tosh UBRR0H _out
    tosl UBRR0L _out
    popt
    NEW
  ]
  _USART_INIT
  USART
;

[F] [DEFINED] _USART1_INIT [P] [IF] \ 2 usarts

  : USART1_SEND ( c -- )
  [
  \ wait for empty transmit buffer
    begin
      UCSR1A zl _in
      UDRE BIT zl andi
    until_=0
  \ Put data into buffer, sends the data
    tosl UDR1 _out
    popt
  ]
  ;

  : USART1 ( -- )
    ['] USART1_SEND IS EMIT
  ;


  : USART1_INIT ( baudrate -- )
    [ tosh UBRR1H _out
      tosl UBRR1L _out
      popt
      NEW
    ]

    _USART1_INIT
    USART1
  ;

[THEN]
