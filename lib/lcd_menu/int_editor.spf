REQUIRE MENU ./menu.spf

0x0
CELL -- IntEditor.setValue
CELL -- IntEditor.getValue
0x1 CHARS -- IntEditor.max
0x1 CHARS -- IntEditor.min
CELL -- IntEditor.prompt
CELL -- IntEditor.display
0x1 CHARS -- IntEditor.immediate
== /IntEditor

VARIABLE CUR-EDITOR
VARIABLE CUR-VALUE
VARIABLE PREV-BUTTONS-TASKS

: CUR-EDITOR@   CUR-EDITOR @ ;

: GET-INT-VALUE ( -- value )
  CUR-VALUE @
;

: ACTUALLY-SET-VALUE ( value -- )
  CUR-VALUE @ CUR-EDITOR@ IntEditor.setValue @EXECUTE
;

: SET-INT-VALUE ( value -- )
  CUR-VALUE !
  CUR-EDITOR@ IntEditor.immediate C@P IF
    ACTUALLY-SET-VALUE
  THEN
;

: INT-EDITOR-DISPLAY
  CUR-EDITOR@ IntEditor.display @EXECUTE
;

: NUMBER-DISPLAY ( -- )
  0 1 AT
  GET-INT-VALUE .
;

: INT-EDITOR-INC-TASK ( -- )
  GET-INT-VALUE ( value )
  DUP CUR-EDITOR@ IntEditor.max C@P < IF
    1+ SET-INT-VALUE
    INT-EDITOR-DISPLAY
  ELSE DROP THEN
;

: INT-EDITOR-DEC-TASK ( -- )
  GET-INT-VALUE ( value )
  DUP CUR-EDITOR@ IntEditor.min C@P > IF
    1- SET-INT-VALUE
    INT-EDITOR-DISPLAY
  ELSE DROP THEN
;

: INT-EDITOR-CANCEL
  PREV-BUTTONS-TASKS @ BUTTONS_TASKS !
  DISPLAY-MENU
;

: INT-EDITOR-OK
  CUR-EDITOR@ IntEditor.immediate C@P IFNOT
    ACTUALLY-SET-VALUE
  THEN
  INT-EDITOR-CANCEL
;

TCREATE INT-EDITOR-TASKS
' INT-EDITOR-DEC-TASK T,   ' INT-EDITOR-INC-TASK T,   ' INT-EDITOR-OK T,   ' INT-EDITOR-CANCEL T,

: INT-EDIT ( editor -- )
  CUR-EDITOR !
  BUTTONS_TASKS @ PREV-BUTTONS-TASKS !
  INT-EDITOR-TASKS BUTTONS_TASKS !
  PAGE
  CUR-EDITOR@ IntEditor.prompt @P COUNTP TYPEP
  CUR-EDITOR@ IntEditor.getValue @EXECUTE  CUR-VALUE !
  INT-EDITOR-DISPLAY
;

' NUMBER-DISPLAY == 'DEFAULT-DISPLAY

:: INT-EDITOR
  PCREATE
  [F]
    T, T, TC, TC, T,
    'DEFAULT-DISPLAY T,
    0x0 TC,
    TALIGN
  [P]
  PDOES>
    INT-EDIT FALSE
  ;
;;

[F]
: SET-DISPLAY ( fn editor -- )
  ?TEXEC   IntEditor.display T!
;

: SET-IMMEDIATE ( b editor -- )
  ?TEXEC   IntEditor.immediate TC!
;

[P]
