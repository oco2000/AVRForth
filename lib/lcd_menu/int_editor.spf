@DECIMAL

REQUIRE MENU ./menu.spf

TREQUIRE (.)
TREQUIRE NOOP

0x0
CELL -- IntEditor.setValue
CELL -- IntEditor.getValue
0x1 CHARS -- IntEditor.max
0x1 CHARS -- IntEditor.min
CELL -- IntEditor.prompt
CELL -- IntEditor.format
CELL -- IntEditor.display
0x1 CHARS -- IntEditor.immediate
== /IntEditor

3000 == EXIT-DURATION

VARIABLE CUR-EDITOR
VARIABLE CUR-VALUE
VARIABLE PREV-BUTTONS-TASKS
VARIABLE ON-EDITOR-CANCEL
VARIABLE EDITOR-TIMERED

: CUR-EDITOR@   CUR-EDITOR @ ;

: GET-MIN-VALUE ( -- value )
  CUR-EDITOR@ IntEditor.min CS@P
;

: GET-MAX-VALUE ( -- value )
  CUR-EDITOR@ IntEditor.max CS@P
;

: GET-INT-VALUE ( -- value )
  CUR-VALUE @   GET-MIN-VALUE GET-MAX-VALUE CLIP
;

: ACTUALLY-SET-VALUE ( value -- )
  GET-INT-VALUE CUR-EDITOR@ IntEditor.setValue @EXECUTE
;

: SET-INT-VALUE ( value -- )
  CUR-VALUE !
  CUR-EDITOR@ IntEditor.immediate C@P IF
    ACTUALLY-SET-VALUE
  THEN
;

: INT-EDITOR-DISPLAY ( -- )
  CUR-EDITOR@ IntEditor.display @EXECUTE
;

: INT-EDITOR-FORMAT ( -- a u )
  GET-INT-VALUE CUR-EDITOR@ IntEditor.format @EXECUTE
;

: DEFAULT-DISPLAY ( -- )
  1 1 AT  INT-EDITOR-FORMAT TYPE SPACE
;

[VECT] INT-EDITOR-CANCEL

: INT-EDITOR-TIMER
  EDITOR-TIMERED @ IF
    ['] INT-EDITOR-CANCEL EXIT-DURATION RTOS_SET_TIMER_TASK
  THEN
;

: INT-EDITOR-CHANGE ( increment -- )
  INT-EDITOR-TIMER
  GET-INT-VALUE + ( new value )
  DUP GET-MAX-VALUE 1+ < IF
    DUP GET-MIN-VALUE >= IF
      SET-INT-VALUE
      INT-EDITOR-DISPLAY
    ELSE DROP THEN
  ELSE DROP THEN
;

: INT-EDITOR-INC-TASK ( -- )
  1 INT-EDITOR-CHANGE
;

: INT-EDITOR-DEC-TASK ( -- )
  -1 INT-EDITOR-CHANGE
;

: (INT-EDITOR-CANCEL)
  ['] INT-EDITOR-CANCEL RTOS_REMOVE_TASK
  PREV-BUTTONS-TASKS @ BUTTONS_TASKS !
  0 Editing C!
  ON-EDITOR-CANCEL @ ?DUP IF EXECUTE ELSE DISPLAY-MENU THEN
;
' (INT-EDITOR-CANCEL) [->] INT-EDITOR-CANCEL

: INT-EDITOR-OK
  CUR-EDITOR@ IntEditor.immediate C@P IFNOT
    ACTUALLY-SET-VALUE
  THEN
  INT-EDITOR-CANCEL
;

TCREATE INT-EDITOR-TASKS
' INT-EDITOR-DEC-TASK T,   ' INT-EDITOR-INC-TASK T,   ' INT-EDITOR-OK T,   ' INT-EDITOR-CANCEL T,
' NOOP T, ' NOOP T, ' NOOP T, ' NOOP T,

: INT-EDIT ( editor -- )
  1 Editing C!
  CUR-EDITOR !
  BUTTONS_TASKS @ PREV-BUTTONS-TASKS !
  INT-EDITOR-TASKS BUTTONS_TASKS !
  PAGE
  CUR-EDITOR@ IntEditor.prompt @P PRINTP
  CUR-EDITOR@ IntEditor.getValue @EXECUTE  CUR-VALUE !
  INT-EDITOR-DISPLAY
  INT-EDITOR-TIMER
;

' DEFAULT-DISPLAY == 'DEFAULT-DISPLAY
' (.) == 'DEFAULT-FORMAT

:: INT-EDITOR
  PCREATE
  [F]
    T, T, TC, TC, T,
    'DEFAULT-FORMAT T,
    'DEFAULT-DISPLAY T,
    0x0 TC,
    TALIGN
  [P]
  PDOES>
    INT-EDIT FALSE
  ;
;;

[F]
: SET-DISPLAY ( fn editor -- )
  ?TEXEC   IntEditor.display T!
;

: SET-FORMAT ( fn editor -- )
  ?TEXEC   IntEditor.format T!
;

: SET-IMMEDIATE ( b editor -- )
  ?TEXEC   IntEditor.immediate TC!
;

[P]

BASE!
