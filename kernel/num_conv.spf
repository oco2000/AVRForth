\ number > string conversion

VARIABLE BASE       ( -- a )    ( base )
VARIABLE HLD        ( -- a )    ( addr in PAD )
0x24 == PAD-SIZE        ( PAD size )
RCREATE PAD   PAD-SIZE ALLOT    ( PAD )
D-HERE [F] @ 1- [P] == NUMPAD   ( PAD end )

: DECIMAL       ( -- )
  0xA BASE !
;

: HEX       ( -- )
  0x10 BASE !
;

: <#     ( -- )             ( start the converstion )
  NUMPAD HLD ! ;
: HOLD   ( char -- )        ( put into PAD )
  HLD @ 1- DUP HLD ! C! ;
: #      ( ud1 -- ud2 )     ( convert one digit )
  BASE @ MU/MOD ROT >DIGIT HOLD ;
: #S     ( ud -- 0 0 )      ( convert all digits )
  BEGIN # 2DUP D0<> INVERT UNTIL ;
: SIGN   ( x -- )           ( put "-", if x < 0 )
  0< IF [ CHAR - ] LITERAL HOLD THEN ;
: #>     ( ud -- addr len ) ( end the conversion )
  2DROP HLD @ NUMPAD OVER - ;
